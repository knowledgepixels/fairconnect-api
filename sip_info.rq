#+ summary: Get info about a particular FAIR Implementation Profile
#+ method: GET
#+ endpoint: https://query.knowledgepixels.com/repo/empty

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

select distinct ?decl_np ?declares_pred ?fsr ?fsr_label where {
  { select ?index_np where {
    service <https://query.knowledgepixels.com/repo/type/LXqnourF1Ytc-SirXlpx7RvaAIRZQvJvwxZHxCx8WKM> {  # fip:Semantic-Interoperability-Profile
      graph npa:graph {
        ?_sipnp_iri npx:hasNanopubType fip:Semantic-Interoperability-Profile ;
          np:hasAssertion ?assertion .
      }
      graph ?assertion {
        ?fip a fip:Semantic-Interoperability-Profile ;
          fip:has-declaration-index ?index_np .
      }
    }
  } }
  { select ?index_np ?decl_np where {
    service <https://query.knowledgepixels.com/repo/type/GMUALeK6SJ-BpFkS9uaEEhVmz3fJNxxVS8O25qWOHQk> { # npx:NanopubIndex
      graph npa:graph {
        ?index_np np:hasAssertion ?assertion .
        ?index_np npx:hasNanopubType npx:NanopubIndex .
      }
      graph ?assertion {
        ?index_np npx:includesElement ?decl_np .
      }
    }
  } }
  { select ?decl_np ?question ?declares_pred ?fsr where {
    service <https://query.knowledgepixels.com/repo/type/ZHGzGTycy1X2wlPcU1D-fVKLZkhVh4voPs5zchzvp_I> { # fip:SIP-Declaration
      graph npa:graph {
        ?decl_np np:hasAssertion ?assertion .
        ?decl_np npx:hasNanopubType fip:SIP-Declaration .
      }
      graph ?assertion {
        values ?declares_pred { fip:declares-current-use-of fip:declares-planned-use-of fip:declares-planned-development-of fip:declares-planned-replacement-of }
        ?subj ?declares_pred ?fsr .
      }
    }
  } }
  optional { select distinct ?fsr (sample(?fsr_label_x) as ?fsr_label_pre) where {
    service <https://query.knowledgepixels.com/repo/type/Bw-poPh6GJvC651MGhT2jxFTjMIim59P1qwCaj0s5XA> { # fip:FAIR-Supporting-Resource
      graph npa:graph {
        ?fsr_np npa:hasValidSignatureForPublicKey ?pubkey ;
          np:hasAssertion ?assertion .
        filter not exists { ?fsr_npx npx:invalidates ?fsr_np ; npa:hasValidSignatureForPublicKey ?pubkey . }
      }
      graph ?assertion {
        ?fsr rdfs:label ?fsr_label_x .
      }
    }
  } group by ?fsr }
  bind(coalesce(?fsr_label_pre, replace(str(?fsr), "^.*[/#]([^/#]*)$", "$1")) as ?fsr_label)
  bind(concat(replace(replace(str(?question), "^.*-([^-MD]+(-[MD]+)?)$", "$1"), "F|M", "0"), "x") as ?sort)
} order by ?sort
