#+ summary: Text search for FAIR Connect
#+ endpoint: https://query.np.trustyuri.net/repo/text
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix search: <http://www.openrdf.org/contrib/lucenesail#>

select ?np ?label ?snippet ?date ?type where {
  graph npa:graph {
    ?np rdfs:label ?label ;
        npa:hasValidSignatureForPublicKey ?pubkey ;
        dct:created ?date .
    filter(strends(str(?date), "Z"))
    optional { ?np npx:hasNanopubType ?type . }
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  ?np search:matches [
    search:query ?_query;
    search:property npa:hasLiteral;
    search:score ?score;
    search:snippet ?snippet ] .
} limit 50
