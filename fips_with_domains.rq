#+ summary: Get all FAIR Implementation Profiles
#+ method: GET
#+ endpoint: https://query.np.trustyuri.net/repo/type/92efd7a0ea4be4e01ec0817ccec87f975203b30addcc3166a204498ffed73b66

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

select distinct ?fipnp ?label ?community (group_concat(distinct ?domain; separator=" ") as ?domains) ?date where {
  graph npa:graph {
    ?fipnp npx:hasNanopubType fip:FAIR-Implementation-Profile .
    ?fipnp npa:hasValidSignatureForPublicKey ?pubkey .
    ?fipnp rdfs:label ?label .
    ?fipnp dct:created ?date .
    ?fipnp npx:introduces ?fip .
    ?fipnp np:hasAssertion ?assertion .
    filter not exists { ?fipnp npx:hasNanopubType npx:ExampleNanopub . }
    filter not exists { ?fipnpx npx:invalidates ?fipnp ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  graph ?assertion {
    ?fip fip:declared-by ?community .
  }
  { select ?community ?domain where { service <https://query.np.trustyuri.net/repo/type/6a09747868afc83837e38d1bac79362fbcc3e2032cff2f2d244f4e24cbd085c6> {   
    graph npa:graph {
      ?cnp npx:hasNanopubType fip:FAIR-Implementation-Community .
      ?cnp npa:hasValidSignatureForPublicKey ?pubkey .
      ?cnp rdfs:label ?label .
      ?cnp dct:created ?date .
      ?cnp npx:introduces ?community .
      ?cnp np:hasAssertion ?assertion .
      filter not exists { ?cnp npx:hasNanopubType npx:ExampleNanopub . }
      filter not exists { ?cnpx npx:invalidates ?cnp ; npa:hasValidSignatureForPublicKey ?pubkey . }
    }
    graph ?assertion {
      ?community fip:has-research-domain ?domain .
      ?community fip:has-research-domain ?__domain_iri .
    }
  } } }
} group by ?fipnp ?label ?community ?date
