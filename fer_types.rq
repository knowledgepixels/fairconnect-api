#+ summary: Get types of FAIR-Enabling Resources
#+ endpoint: https://query.np.trustyuri.net/repo/empty
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>

select ?type (count(distinct ?np) as ?count) where {

  {

    bind(iri(concat("https://query.np.trustyuri.net/repo/type/", sha256(str(fip:FAIR-Enabling-Resource)))) as ?fer_service)
    service ?fer_service {
      graph npa:graph {
        ?np npa:hasValidSignatureForPublicKey ?pubkey ;
          npx:hasNanopubType fip:FAIR-Enabling-Resource .
        ?np dct:created ?date . filter(str(?date) > "2022")
        filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
        filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
        ?np npx:hasNanopubType ?type .
        values ?type {
          fip:Authentication-and-authorization-service
          fip:Editor
          fip:FAIR-Representation-Service
          fip:FAIR-Supporting-Software
          fip:Identifier-service
          fip:Provenance-Tracking-Service
          fip:Registry
          fip:Validation-Service
          fip:Web-API
          fip:FAIR-Specification
          fip:Communication-protocol
          fip:Crosswalk
          fip:FAIR-Implementation-Profile
          fip:Knowledge-representation-language
          fip:Metadata-schema
          fip:Metadata-data-linking-schema
          fip:Provenance-model
          fip:Semantic-model
          fip:Structured-vocabulary
          fip:FAIR-Data-Policy
          fip:Data-usage-license
          fip:Metadata-preservation-policy
          fip:Persistency-Policy
          fip:FAIR-Practice
        }
      }
    }

  } union {

    bind(iri(concat("https://query.np.trustyuri.net/repo/type/", sha256(str(fip:FAIR-Supporting-Resource)))) as ?fer_service)
    service ?fer_service {
      graph npa:graph {
        ?np npa:hasValidSignatureForPublicKey ?pubkey ;
          npx:hasNanopubType fip:FAIR-Supporting-Resource .
        ?np dct:created ?date . filter(str(?date) > "2022")
        filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
        filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
        ?np npx:hasNanopubType ?type .
        values ?type {
          fip:Authentication-and-authorization-service
          fip:Editor
          fip:FAIR-Representation-Service
          fip:FAIR-Supporting-Software
          fip:Identifier-service
          fip:Provenance-Tracking-Service
          fip:Registry
          fip:Validation-Service
          fip:Web-API
          fip:FAIR-Specification
          fip:Communication-protocol
          fip:Crosswalk
          fip:FAIR-Implementation-Profile
          fip:Knowledge-representation-language
          fip:Metadata-schema
          fip:Metadata-data-linking-schema
          fip:Provenance-model
          fip:Semantic-model
          fip:Structured-vocabulary
          fip:FAIR-Data-Policy
          fip:Data-usage-license
          fip:Metadata-preservation-policy
          fip:Persistency-Policy
          fip:FAIR-Practice
        }
      }
    }

  }

}
group by ?type
order by desc(?count)
