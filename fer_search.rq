#+ summary: Full-text search for FAIR-Enabling Resources
#+ endpoint: https://query.knowledgepixels.com/repo/text
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>
prefix search: <http://www.openrdf.org/contrib/lucenesail#>

select distinct ?np ?description ?label ?date (group_concat(distinct ?type; separator=" ") as ?types) ?qualifier (group_concat(distinct ?community; separator=" ") as ?communities) (max(?score) as ?maxscore) where {
  graph npa:graph {
    ?np rdfs:label ?label ;
        npa:hasValidSignatureForPublicKey ?pubkey ;
        dct:created ?date .
    filter(strends(str(?date), "Z"))
    filter exists { ?np npx:hasNanopubType fip:FAIR-Enabling-Resource . }
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
    ?np npx:hasNanopubType ?type .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  optional { service <https://query.knowledgepixels.com/repo/full> {
    graph npa:graph {
      ?np np:hasAssertion ?a .
      ?np np:hasPublicationInfo ?i .
    }
    graph ?a {
      ?thing rdfs:comment ?description .
    }
    graph ?i {
      ?np npx:introduces ?thing .
    }
  } }
  ?np search:matches [
    search:query ?_query ;
    search:property rdfs:label ;
    search:score ?score ;
    search:snippet ?snippet ] .
  optional { service <https://query.knowledgepixels.com/repo/type/zVGjrD4Qn9EqrNWjJOFOv_ZEziCckW5CUKP1DC3fAFE> {  # (http://purl.org/nanopub/x/qualifies)
    graph npa:graph {
      ?qualification_np np:hasAssertion ?qa ;
        npa:hasValidSignatureForPublicKey ?qpubkey .
      filter not exists { ?qualification_npx npx:invalidates ?qualification_np ; npa:hasValidSignatureForPublicKey ?qpubkey . }
    }
    graph <https://w3id.org/np/RAB1C3avoJ2dNztf68MyU5hyyykf2MEiyUc2lMKq_0OTA#assertion> {
      ?qpubkeys npx:hasPublicKey ?qpubkey .
    }
    graph ?qa {
      ?qualifier npx:qualifies ?np .
    }
  } }
  filter not exists { service <https://query.knowledgepixels.com/repo/type/OWkodtCniByC6aLLbciFn7Fvpp-0HOoqzi-PpwvFKu0> {  # (http://purl.org/nanopub/x/disapprovesOf)
    graph npa:graph {
      ?disapproval_np np:hasAssertion ?da ;
        npa:hasValidSignatureForPublicKey ?dpubkey .
      filter not exists { ?disapproval_npx npx:invalidates ?disapproval_np ; npa:hasValidSignatureForPublicKey ?dpubkey . }
    }
    graph <https://w3id.org/np/RAB1C3avoJ2dNztf68MyU5hyyykf2MEiyUc2lMKq_0OTA#assertion> {
      ?qpubkeys npx:hasPublicKey ?dpubkey .
    }
    graph ?da {
      ?disapprover npx:disapprovesOf ?np .
    }
  } }
  optional { service <https://query.knowledgepixels.com/repo/type/SthF6GCm2zppFgrbeuyJTmIigP-YgeQaYAdWrRq8-iM> {  # (https://w3id.org/fair/fip/terms/FIP-Declaration)
    graph npa:graph {
      ?decl_np np:hasAssertion ?decl_a ;
        npa:hasValidSignatureForPublicKey ?decl_pubkey .
      filter not exists { ?decl_npx npx:invalidates ?decl_np ; npa:hasValidSignatureForPublicKey ?decl_pubkey . }
    }
    graph ?decl_a {
      ?decl a fip:FIP-Declaration .
      # ----
      # not really needed but seems to make query faster:
      values ?use { fip:declares-current-use-of fip:declares-planned-use-of fip:declares-planned-development-of fip:declares-planned-replacement-of }
      ?decl ?use ?thing .
      # ----
      ?decl fip:declared-by ?community .
    }
  } }
}
group by ?np ?description ?label ?date ?qualifier
order by desc(?maxscore)
