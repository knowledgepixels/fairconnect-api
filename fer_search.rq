#+ summary: Full-text search for FAIR-Enabling Resources
#+ endpoint: https://query.np.trustyuri.net/repo/text
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>
prefix search: <http://www.openrdf.org/contrib/lucenesail#>

select distinct ?np ?thing ?description ?label ?date ?types ?qualifier (group_concat(distinct ?community; separator=" ") as ?communities) ?maxscore where {

  optional { <x:> <x:> ?__endpoint_iri } .
  bind(coalesce(?__endpoint_iri, <https://query.np.trustyuri.net/repo/empty>) as ?endpoint)
  bind(replace(str(?endpoint), "repo/[a-zA-Z0-9]+$", "") as ?service)

  { select ?np ?thing ?label ?date (group_concat(distinct ?type; separator=" ") as ?types) (max(?score) as ?maxscore) ?latest_curators_assertion where {

    bind(coalesce(?__endpoint_iri, <https://query.np.trustyuri.net/repo/empty>) as ?endpoint)
    bind(replace(str(?endpoint), "repo/[a-zA-Z0-9]+$", "") as ?service)

  graph npa:graph {
    <https://w3id.org/np/RA27Uhopq4MHZziL2lKXX-wTb1jz4KVLbHaupxyAcCt9Y> npa:hasValidSignatureForPublicKey ?curators_np_pk .
    ?latest_curators_np npa:hasValidSignatureForPublicKey ?curators_np_pk .
    filter not exists { ?latest_curators_npx npx:invalidates ?latest_curators_np ; npa:hasValidSignatureForPublicKey ?curators_np_pk . }
    ?latest_curators_np np:hasAssertion ?latest_curators_assertion .
  }
  graph npa:networkGraph {
    ?latest_curators_np (npx:supersedes)* <https://w3id.org/np/RA27Uhopq4MHZziL2lKXX-wTb1jz4KVLbHaupxyAcCt9Y> .
    filter not exists { ?latest_curators_npxx npx:supersedes ?latest_curators_np }
  }
        
      graph npa:graph {
        ?np rdfs:label ?label ;
            npa:hasValidSignatureForPublicKey ?pubkey ;
            dct:created ?date .
        ?np npx:introduces|npx:describes ?thing .
        filter(str(?date) > "2022")
        filter exists { ?np npx:hasNanopubType ?fsr_type . values ?fsr_type { fip:FAIR-Enabling-Resource fip:FAIR-Supporting-Resource fip:Available-FAIR-Enabling-Resource } }
        filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
        ?np npx:hasNanopubType ?__type_iri .
        ?np npx:hasNanopubType ?type .
        filter(?type != fip:FAIR-Enabling-Resource)
        filter(?type != fip:Available-FAIR-Enabling-Resource)
        filter(?type != fip:FAIR-Enabling-Resource-to-be-Developed)
        filter(?type != fip:FAIR-Supporting-Resource)
        filter(?type != fip:Available-FAIR-Supporting-Resource)
        filter(?type != fip:FAIR-Supporting-Resource-to-be-Developed)
        filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
      }
      ?np search:matches [
        search:query ?_query ;
        search:property rdfs:label ;
        search:score ?score ;
        search:snippet ?snippet ] .

    bind(iri(concat(?service, "repo/type/", sha256("http://purl.org/nanopub/x/disapprovesOf"))) as ?disappr_service)
    filter not exists { service ?disappr_service {
      graph npa:graph {
        ?disapproval_np np:hasAssertion ?da ;
          npa:hasValidSignatureForPublicKey ?dpubkey .
        filter not exists { ?disapproval_npx npx:invalidates ?disapproval_np ; npa:hasValidSignatureForPublicKey ?dpubkey . }
      }
      graph ?latest_curators_assertion {
        ?qpubkeys npx:hasPublicKey ?dpubkey .
      }
      graph ?da {
        ?disapprover npx:disapprovesOf ?np .
      }
    } }

  } group by ?np ?thing ?label ?date ?latest_curators_assertion order by desc(?maxscore) limit 20 }
    
  bind(iri(concat(?service, "repo/full")) as ?full_service)
  optional { service ?full_service {
    graph npa:graph {
      ?np np:hasAssertion ?a .
      ?np np:hasPublicationInfo ?i .
    }
    graph ?a {
      ?thing rdfs:comment ?description .
    }
  } }

  bind(iri(concat(?service, "repo/type/", sha256("http://purl.org/nanopub/x/qualifies"))) as ?qualifies_service)
  optional { service ?qualifies_service {
    graph npa:graph {
      ?qualification_np np:hasAssertion ?qa ;
        npa:hasValidSignatureForPublicKey ?qpubkey .
      filter not exists { ?qualification_npx npx:invalidates ?qualification_np ; npa:hasValidSignatureForPublicKey ?qpubkey . }
    }
    graph ?latest_curators_assertion {
      ?qpubkeys npx:hasPublicKey ?qpubkey .
    }
    graph ?qa {
      ?qualifier npx:qualifies ?np .
    }
  } }


  bind(iri(concat(?service, "repo/type/", sha256("https://w3id.org/fair/fip/terms/FIP-Declaration"))) as ?fip_service)
  optional { service ?fip_service {
    graph npa:graph {
      ?decl_np np:hasAssertion ?decl_a ;
        npa:hasValidSignatureForPublicKey ?decl_pubkey .
      filter not exists { ?decl_npx npx:invalidates ?decl_np ; npa:hasValidSignatureForPublicKey ?decl_pubkey . }
    }
    graph ?decl_a {
      ?decl a fip:FIP-Declaration .
      # ----
      # not really needed but seems to make query faster:
      values ?use { fip:declares-current-use-of fip:declares-planned-use-of fip:declares-planned-development-of fip:declares-planned-replacement-of }
      ?decl ?use ?thing .
      # ----
      ?decl fip:declared-by ?community .
    }
  } }

} group by ?np ?thing ?description ?label ?date ?types ?qualifier ?maxscore order by desc(?maxscore)
