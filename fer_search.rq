#+ summary: Full-text search for FAIR-Enabling Resources
#+ endpoint: https://query.knowledgepixels.com/repo/text
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix fip: <https://w3id.org/fair/fip/terms/>
prefix search: <http://www.openrdf.org/contrib/lucenesail#>

select distinct ?np ?description ?label ?date (group_concat(distinct ?type; separator=" ") as ?types) ?qualifier (max(?score) as ?maxscore) where {
  graph npa:graph {
    ?np rdfs:label ?label ;
        npa:hasValidSignatureForPublicKey ?pubkey ;
        dct:created ?date .
    filter(strends(str(?date), "Z"))
    filter exists { ?np npx:hasNanopubType fip:FAIR-Enabling-Resource . }
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
    ?np npx:hasNanopubType ?type .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  optional { service <https://query.knowledgepixels.com/repo/full> {
    graph npa:graph {
      ?np np:hasAssertion ?a .
      ?np np:hasPublicationInfo ?i .
    }
    graph ?a {
      ?thing rdfs:comment ?description .
    }
    graph ?i {
      ?np npx:introduces ?thing .
    }
  } }
  ?np search:matches [
    search:query ?_query ;
    search:property rdfs:label ;
    search:score ?score ;
    search:snippet ?snippet ] .
  optional { service <https://query.knowledgepixels.com/repo/type/zVGjrD4Qn9EqrNWjJOFOv_ZEziCckW5CUKP1DC3fAFE> {
    graph npa:graph {
      ?qualification_np npa:hasHeadGraph ?qh ;
        np:hasAssertion ?qa ;
        npa:hasValidSignatureForPublicKey ?qpubkey .
      filter not exists { ?qualification_npx npx:invalidates ?qualification_np ; npa:hasValidSignatureForPublicKey ?qpubkey . }
    }
    graph <https://w3id.org/np/RAB1C3avoJ2dNztf68MyU5hyyykf2MEiyUc2lMKq_0OTA#assertion> {
      ?qpubkeys npx:hasPublicKey ?qpubkey .
    }
    graph ?qh {
      ?qualification_np np:hasAssertion ?qa .
    }
    graph ?qa {
      ?qualifier npx:qualifies ?np .
    }
  } }
}
group by ?np ?description ?label ?date ?qualifier
order by desc(?maxscore)
