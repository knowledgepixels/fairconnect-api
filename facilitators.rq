#+ summary: Get the list of all GO FAIR facilitators
#+ endpoint: https://query.np.trustyuri.net/repo/type/16fb6a38f21730f443f6fc7c4a7340a54364973a0b078db497206a13fc5d33f4
#+ method: GET

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?facilitatorName ?facilitatorId ?pubkey where {
  graph <https://w3id.org/np/RA5PyVZnfPpd5ozkVjzQQbWyfLXCmeApLfhsyl8Rgnmh8#assertion> {
    ?pubkeys npx:hasPublicKey ?pubkey .
  }
  service <https://query.np.trustyuri.net/repo/type/77757cabf6184c51c20b8b0fe5dc5e1365b7f628448335184ad54319a0affdfc> {  # (http://purl.org/nanopub/x/declaredBy)
    graph npa:graph {
      ?np npa:hasValidSignatureForPublicKey ?pubkey ;
        np:hasAssertion ?a .
      filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    }
    graph ?a {
      ?decl npx:hasPublicKey ?pubkey ;
        npx:declaredBy ?facilitatorId .
      ?facilitatorId foaf:name ?facilitatorName .
    }
  }
} order by ?facilitatorName
